apply plugin: 'com.android.application'
apply plugin: 'findbugs'

findbugs {
    sourceSets = []
    ignoreFailures = false
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "me.hackerchick.catima"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 51
        versionName "1.5.1"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    lintOptions {
        disable "GoogleAppIndexingWarning"
        disable "ButtonStyle"
        disable "AlwaysShowAction"
        disable "MissingTranslation"
        disable "MissingPrefix"
    }

    // Starting with Android Studio 3 Robolectric is unable to find resources.
    // The following allows it to find the resources.
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0@aar'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'org.apache.commons:commons-csv:1.5'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.jaredrummler:colorpicker:1.0.2'
    implementation group: 'com.google.guava', name: 'guava', version: '20.0'
    implementation "com.vanniktech:vntnumberpickerpreference:1.0.0"
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.0.2"
}

task findbugs(type: FindBugs, dependsOn: 'assembleDebug') {

    description 'Run findbugs'
    group 'verification'

    classes = fileTree('build/intermediates/javac/debug/compileDebugJavaWithJavac/classes')
    source = fileTree('src/main/java')
    classpath = files()

    effort = 'max'

    excludeFilter = file("./config/findbugs/exclude.xml")

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
