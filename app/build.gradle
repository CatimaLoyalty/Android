import com.github.spotbugs.snom.SpotBugsTask

apply plugin: 'com.android.application'
apply plugin: 'com.github.spotbugs'

spotbugs {
    ignoreFailures = false
    effort = 'max'
    excludeFilter = file("./config/spotbugs/exclude.xml")
    reportsDir = file("$buildDir/reports/spotbugs/")
}

android {
    compileSdkVersion 29
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "me.hackerchick.catima"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 60
        versionName "1.9.1"

        vectorDrawables.useSupportLibrary true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Catima"
        }
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Catima Debug"
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable "GoogleAppIndexingWarning", "ButtonStyle", "AlwaysShowAction",
                "MissingTranslation", "MissingPrefix"
    }

    // Starting with Android Studio 3 Robolectric is unable to find resources.
    // The following allows it to find the resources.
    testOptions {
        unitTests {
            all {
                testLogging {
                    events 'started', 'passed', 'skipped', 'failed'
                }
            }
            includeAndroidResources true
        }
    }
}

dependencies {
// AndroidX
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'io.wcm.tooling.spotbugs:io.wcm.tooling.spotbugs.annotations:1.0.0'

// Third-party
    implementation 'com.journeyapps:zxing-android-embedded:4.1.0@aar'
    implementation 'com.google.zxing:core:3.3.3' // Do not upgrade past 3.3.3! Causes a crash on versions before Android 7
    implementation 'org.apache.commons:commons-csv:1.8'
    implementation 'com.jaredrummler:colorpicker:1.1.0'
    implementation 'com.google.guava:guava:30.1-jre'
    implementation 'com.github.invissvenska:NumberPickerPreference:1.0.1'

// Testing
    testImplementation 'androidx.test:core:1.0.0'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.robolectric:robolectric:4.4'
}

tasks.withType(SpotBugsTask) {

    description 'Run spotbugs'
    group 'verification'

    //classes = fileTree('build/intermediates/javac/debug/compileDebugJavaWithJavac/classes')
    //source = fileTree('src/main/java')
    //classpath = files()

    reports {
        xml.enabled = false
        html.enabled = true
    }
}
